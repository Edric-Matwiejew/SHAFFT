@PACKAGE_INIT@

# shafftConfig.cmake - Package configuration for SHAFFT
# Provides: shafft::shafftc++ (C++ API), shafft::shafftc (C API)

include(CMakeFindDependencyMacro)

# Required dependencies
find_dependency(MPI REQUIRED)

# Backend-specific dependencies
set(SHAFFT_BACKEND_HIPFFT @SHAFFT_ENABLE_HIPFFT@)
set(SHAFFT_BACKEND_FFTW @SHAFFT_ENABLE_FFTW@)

if(SHAFFT_BACKEND_HIPFFT)
  find_dependency(hip REQUIRED)
  find_dependency(hipfft REQUIRED)
endif()

if(SHAFFT_BACKEND_FFTW)
  # FFTW discovery - try pkg-config first, then FindFFTW
  find_package(PkgConfig QUIET)
  if(PkgConfig_FOUND)
    pkg_check_modules(FFTW3 QUIET IMPORTED_TARGET fftw3)
    pkg_check_modules(FFTW3F QUIET IMPORTED_TARGET fftw3f)
  endif()
  if(NOT FFTW3_FOUND)
    find_dependency(FFTW REQUIRED COMPONENTS DOUBLE_LIB FLOAT_LIB)
  endif()
endif()

# Import targets
include("${CMAKE_CURRENT_LIST_DIR}/shafftTargets.cmake")

# Provide version info
set(SHAFFT_VERSION @PROJECT_VERSION@)
set(SHAFFT_VERSION_MAJOR @PROJECT_VERSION_MAJOR@)
set(SHAFFT_VERSION_MINOR @PROJECT_VERSION_MINOR@)
set(SHAFFT_VERSION_PATCH @PROJECT_VERSION_PATCH@)

check_required_components(shafft)
